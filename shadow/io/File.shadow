/**
 * Class {@code File} provides utilities for interacting with a file in the
 * operating system, such as creating a new file, deleting an existing file,
 * reading from a file, and writing to a file.
 */
class shadow:io@
	File
is	CanRead
and CanWrite
and CanClose
{
	/// handle contains OS-specific file handle information 
	long handle = -1L;
	get Path path;
	
	[ImportAssembly]
	private readonly __shadowFile_Exists(String path) => (boolean);
	[ImportAssembly]
	private readonly __shadowFile_Close(long handle) => ();
	
	/**
	 * Creates a {@code File} object associated with the file on the given
	 * path.
	 * @param path of the file
	 */
	public create( String path )
	{
		this(Path:create(path));
	}
	
	/**
	 * Creates a {@code File} object associated with the file specified by the
	 * given {@code Path} object.
	 * @param path of the file
	 */
	public create( Path path )
	{
		this:path = path;
	}
	
	/**
	 * Closes the {@code File} on destroy.
	 */
	public destroy
	{
		close();
	}
	
	/**
	 * Returns a {@code String} representation of the path of this file.
	 */
	public readonly toString() => ( String )
	{
		return #path;
	}

	/**
	 * Determines a file in the operating system matches the
	 * path in this {@code File} object.
	 * @return {@code true} if the file exists
	 */	
	public readonly get exists() => ( boolean )
	{
		return __shadowFile_Exists(#path);
	}
	
	/**
	 * Creates or deletes a file in the operating system with the path of 
	 * this {@code File} object.
	 * @param createOrDelete {@code true} if the file should be created,
	 * {@code false} if it should be deleted
	 */	
	[ImportNative]
	public set exists( boolean createOrDelete ) => ();
	
	/**
	 * Gets the current position of the read/write pointer inside the file. 
	 * @return position in file
	 */	
	[ImportNative]
	public get readonly position() => ( long );
	
	/**
	 * Sets the current position of the read/write pointer inside the file. 
	 * @param position in file
	 */
	[ImportNative]
	public set position( long position ) => ();
	
	/**
	 * Gets the size of the file in bytes. 
	 * @return file size
	 */
	[ImportNative]
	public get readonly size() => ( long );
	
	/**
	 * Sets the size of the file in bytes, extending or truncating it. 
	 * @param new file size
	 */
	[ImportNative]
	public set size( long size ) => ();
	
	/**
	 * Reads data from the file into the buffer and updates the read pointer
	 * location.	     
	 * @param buffer array of {@code byte} values to hold file data
	 * @return number of {@code byte} values successfully read, always less
	 * than or equal the length of the buffer
	 */
	[ImportNative]
	public readonly read( byte[] buffer ) => ( long );
	
	/**
	 * Writes data from the buffer into the file and updates the write pointer
	 * location.	     
	 * @param buffer array of {@code byte} values to be written
	 * @return number of {@code byte} values successfully written
	 */
	[ImportNative]
	public write( readonly byte[] buffer ) => ( long );
	
	/**
	 * Closes the file.
	 */
	public close() => ()
	{
		if(handle != -1L) {
			__shadowFile_Close(handle);
			handle = -1L;
		}
	}
	
	// C FRAMEWORK EXTENSIONS
	[ExportAssembly]
	private _shadowFile_SetHandle(long handle) => ()
	{
		this:handle = handle;
	}
}
