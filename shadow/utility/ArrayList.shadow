import shadow:io@Console;

class shadow:utility@
ArrayList<V is CanEqual<V>>
implements List<V>
{
	get int size = 0;
	V[] elements = V:create[10];
	int modifications = 0;
	
	public isEmpty() => ( boolean )
	{
		return size == 0;
	}	
	
	public readonly index(int index) => (V value)
	{
		if( index < 0 or index >= size )
			throw IndexOutOfBoundsException:create();
		
		try
		{
			return check(elements[index]);
		}
		recover
		{
			//should never happen
			throw UnexpectedNullException:create();
		}
	}
	
	
	public index(int index, V value) => ()
	{
		if( index < 0 or index > size )
			throw IndexOutOfBoundsException:create();
			
		if( index == size )
		{
			if( size >= elements->size )
				grow();
			size += 1;		
		}
			
		elements[index] = value;
		modifications += 1;	
	}
	
	private grow() => ()
	{
		V[] temp = V:create[elements->size * 2];
		for( int i = 0; i < elements->size; i += 1 )
			temp[i] = elements[i];
		elements = temp;	
	}

	public add(V value) => ( ArrayList<V> )
	{
		if( size >= elements->size )
			grow();
		elements[size] = value;
		size += 1;
		modifications += 1;
		
		return this;			
	}
		
	public clear() => ( ArrayList<V> )
	{
		size = 0;
		modifications += 1;
		return this;
	}
	
	public readonly contains(V value) => (boolean found)
	{				
		return indexOf(value) != -1;	
	}
	
	public readonly indexOf(V value) => (int index)
	{		
		try
		{
			for( int i = 0; i < size; i += 1 )
			{
				if( check(elements[i]) == value )
					return i;
			}
		}
		recover
		{}
		
		return -1;
	}
	
	public delete(int index) => ( V value )
	{
		if( index < 0 or index >= size )
			throw IndexOutOfBoundsException:create();
	
		try
		{
			V temp = check(elements[index]);	
			for( int i = index; i < size - 1; i += 1 )
				elements[i] = elements[i + 1];
			size -= 1;
			modifications += 1;
			return temp;
		}
		recover
		{
			throw UnexpectedNullException:create();
		}
	}	
	
	public remove( V value ) => (boolean success)
	{
		int index = indexOf(value);
		if( index == -1 )
			return false;
			
		delete(index);
		return true;	
	}
	
	public readonly toString() => (String)
	{
		MutableString string = MutableString:create("[");
		boolean first = true;
		for( int i = 0; i < size; i += 1 )
		{
			if( first )
				first = false;
			else
				string.append(", ");
			string.append(#index(i));
		}
		
		return string.append("]").toString();
	}

	public readonly iterator() => (Iterator<V>)
	{
		return ArrayListIterator:create();
	}
	
	private class ArrayListIterator implements Iterator<V>
	{
		int index = 0;
		int expectedModifications = modifications;
		
		private immutable checkForModifications() => ()
		{
			if( expectedModifications != modifications )
				throw IllegalModificationException:create();		
		}
	
		public immutable hasNext() => (boolean)
		{
			checkForModifications();
			return index < size;
		}
		
		public next() => (V)
		{
			checkForModifications();
			if( index >= size )
				throw IndexOutOfBoundsException:create();
			try
			{
				V temp = check(elements[index]);
				index += 1;
				return temp;		
			}
			recover
			{
				//should never happen
				throw UnexpectedNullException:create();
			}
		}
	}

}