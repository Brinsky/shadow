class shadow:utility@LinkedList<V is CanEqual<V>> implements List<V>, Deque<V>
{
	get int size = 0;
	Node header = Node:create();
	int modifications = 0;

	private class Node
	{
		get Node prev = this, next = this;
		get set nullable V value = null;
		public create() { }
		public create( V initialValue, Node after )
		{
			( after:prev:next, prev,       next,  after:prev ) =
			( this,            after:prev, after, this       );
			value = initialValue;
			size += 1;
			modifications += 1;
		}
		public set value( V initialValue ) => ()
		{
			value = initialValue;
			modifications += 1;
		}
		public clear() => ()
		{
			( prev, next ) = ( this, this );
			size = 0;
			modifications += 1;
		}
		public delete() => ()
		{
			( prev:next, next:prev ) = ( next, prev );
			size -= 1;
			modifications += 1;
		}
	}
	private findIndex( int index ) => ( Node node )
	{
		if ( cast<uint>(index) >= cast<uint>(size) )
			throw IndexOutOfBoundsException:create();
		Node current = header;
		if ( index < size / 2 )
		{
			while ( index >= 0 )
			{
				current = current->next;
				index -= 1;
			}
		}
		else
		{
			while ( index != size )
			{
				current = current->prev;
				index += 1;
			}
		}
		return current;
	}
	private readonly findNode( V value ) => ( int index, nullable Node node )
	{
		int index = 0;
		Node current = header;
		try
		{
			while (true)
			{
				current = current->next;
				if ( current === header )
					return ( -1, null );
				if ( check(current->value) == value )
					return ( index, current );
				index += 1;
			}
		}
		recover
		{
			throw UnexpectedNullException:create();
		}
	}
	
	public isEmpty() => ( boolean )
	{
		return size == 0;
	}

	public index( int index ) => ( V value )
	{
		try
		{
			return check(findIndex(index)->value);
		}
		recover
		{
			throw UnexpectedNullException:create();
		}
	}
	public index( int index, V value ) => ()
	{
		if ( index == size )
			add(value);
		else
			findIndex(index)->value = value;
	}

	public add( V value ) => ( LinkedList<V> )
	{
		Node:create(value, header);
		return this;
	}
	public remove( V value ) => ( boolean removed )
	{
		try
		{
			nullable Node node;
			(, node) = findNode(value);
			check(node).delete();
			return true;
		}
		recover
		{
			return false;
		}
	}
	public delete( int index ) => ( V value )
	{
		Node node = findIndex(index);
		try
		{
			V value = check(node->value);
			node.delete();
			return value;
		}
		recover
		{
			throw UnexpectedNullException:create();
		}
	}
	public readonly indexOf( V value ) => ( int index )
	{
		int index;
		(index, ) = findNode(value);
		return index;
	}
	public readonly contains( V value ) => ( boolean found )
	{		 
		return indexOf(value) != -1;
	}
	public clear() => ( LinkedList<V> )
	{
		header.clear();
		return this;
	}

	public readonly iterator() => ( Iterator<V> )
	{
		return LinkedListIterator:create();
	}
	
	public addFirst( V element ) => ( LinkedList<V> )
	{
		Node node = findIndex( 0 );
		Node:create( element, node );
		return this;
	}
	
	public addLast( V element ) => ( LinkedList<V> )
	{
		return add( element );
	}
	
	public getFirst() => (V element )
	{
		return index(0);
	}
	
	public getLast() => (V element )
	{
		return index(size - 1);
	}
	
	public removeFirst() => (V element )
	{
		return delete(0);
	}
	
	public removeLast() => ( V element )
	{
		return delete(size - 1);
	}	
	
	public readonly toString() => ( String )
	{	
		var string = MutableString:create("[");
		var i = iterator();				
		
		boolean first = true;
		while( i.hasNext() )
		{
			if( first )
				first = false;
			else
				string.append(", ");
			
			string.append(#i.next());			
		}
		
		return string.append("]").toString();
	}
	
	
	private class LinkedListIterator implements Iterator<V>
	{
		Node current = header->next;
		int expectedModifications = modifications;

		private readonly checkForModifications() => ()
		{
			if ( modifications != expectedModifications )
				throw IllegalModificationException:create();
		}
		public readonly hasNext() => ( boolean )
		{
			checkForModifications();
			return current !== header;
		}
		public next() => ( V value )
		{
			checkForModifications();
			if ( !hasNext() )
				throw IndexOutOfBoundsException:create();
			try
			{				
				V value = check(current->value);			
				current = current->next;			
				return value;
			}
			recover
			{
				throw UnexpectedNullException:create();
			}
		}
	}
}
