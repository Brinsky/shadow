abstract locked class shadow:standard@Array<T> implements CanIndexStore<int[],T>, CanIterate<T>
{
	get immutable int[] lengths;
	Object data;
	protected create( immutable int[] lengths, Object data )	
	{
		this:lengths = lengths;		
		this:data = data;		
	}	
	
	protected native create( immutable int[] lengths );
	
	/*
	protected native create( immutable int[] lengths )
	{
		this:lengths = lengths;
		this:data = allocate(T:class, this->size);
	}
	*/	
		
	//private immutable native allocate( Class type, int size ) => ( Object );

	public readonly get size() => ( int )
	{
		int size = lengths[0];
		for ( int i = 1; i < this->dims; i += 1 )
			size *= lengths[i];
		return size;
	}
	
	public readonly native get dims() => ( int );
	
	
	/* //copy implementation based off the following code
	private readonly copyProxy(AddressMap addresses) => ( Array<T> )
	{
		Array<T> duplicate = Array<T>:create( lengths );
		for( int i = 0; i < this->size; i += 1 )
			duplicate.index(i, index(i).copy(addresses)));
				
		return duplicate;
	}
	*/
	
	//only makes sense for 1D arrays?
	public readonly native subarray( int start, int end ) => ( Array<T> );

	public readonly native index( int[] indices ) => (T);
	public native index( int[] indices, T value ) => ();
	
	//private methods that take the final offset after all the dimensions have been computed
	//unsafe to give to the user, but useful for internals
	private readonly native index( int index ) => (T);
	private native index( int index, T value) => ();	

	public native readonly getBaseClass() => ( Class );

	public readonly toString() => ( String )
	{
		if ( this->size == 0 )
			return "[]";
		
		MutableString string = MutableString:create("[");						
		Class baseClass = getBaseClass();
		boolean first = true;
		foreach( T value in this )
		{	
			if ( first )
				first = false;
			else
				string.append(", ");
				
			string.append(value);
		}
		return string.append("]").toString();
	}
	
	public readonly iterator() => (Iterator<T>)
	{
		return ArrayIterator:create();
	} 
	
	private class ArrayIterator implements Iterator<T>
	{
		int position = 0;
		int limit = size();
		
		public readonly hasNext() => (boolean)
		{	
			return position < limit;		
		}
		
		public next() => (T)
		{
			if( position >= limit )
				throw IndexOutOfBoundsException:create();
			
			T value = index( position );			
			position += 1;			
			
			return value;  
		}
	}
}
