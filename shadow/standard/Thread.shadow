import shadow:io@Console;
import shadow:utility;
import shadow:natives;

/**
 * @author Claude Abounegm
 */
locked class shadow:standard@
	Thread
is CanEqual<Thread>
and CanIterate<Thread>
and CanIndex<int, Thread>
{
	// START: Native methods
	private readonly native spawnThread(Pointer ptr) => (int);
	//private readonly native joinThread(Pointer ptr) => (int);
	
	private readonly native get handleSize() => (int);
	private native get staticNextId() => (int);
	// END: Native methods
	
	// START: Private variables
	nullable Exception ex = null;	
	boolean joined;
	Handle handle;
	Mutex mutex;
	CanRun runner;
	ThreadSafeList<Thread> childrenList;
	Signaler signaler;
	// END: Private variables
	
	/**
	* Gets the Shadow-specific thread ID.
	*/
	get int id;
	
	/**
	* Gets the {@code Thread} which spawned this thread. The Main thread does not
	* have a parent, and is null.
	*/
	get nullable Thread parent = null;
	
	/**
	* Gets the name of the thread set at spawn time. If no name is set
	* at spawn time, the name takes the format "Thread#{id}".
	*/
	get String name;

	/**
	* Creates a new {@code Thread} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread.
	* 
	* @param runner The instance that contains a run function.
	*/
	public create(CanRun runner)
	{
		this(runner, null);
	}
	
	/**
	* Creates a new {@code Thread} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread, and takes a {@code String} which is the name
	* of the thread for reference purposes.
	* 
	* @param runner The instance that contains a run function.
	* @param name The name of the thread.
	*/
	public create(CanRun runner, nullable String name)
	{
		this:id = this->staticNextId;
		this:childrenList = ThreadSafeList<Thread>:create();
		
		try {
			this:name = check(name);
		} recover {
			this:name = "Thread#" # id;
		}
		
		this:joined = false;
		this:mutex = Mutex:create();
		this:signaler = Signaler:create();
		
		if(this:id == 0) {
			this:runner = runner;
			this:handle = Handle:create();
		} else {
			this:handle = Handle:create(this->handleSize);
			this:runner = copy(runner);
			this:parent = curthread();
			addToParent();
			
			// spawn the thread and give it the handle
			spawnThread(handle->ptr);
		}
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates.
	* Joining on a dead thread, or on the main thread has no side effects.
	* If the thread is terminated because of an exception, the exception is thrown
	* when this method is called.
	* 
	* @throws InvalidOperationException if this is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	*/
	public join() => () 
	{
		join(false, Time->InfiniteTimeout);
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates or until it times out.
	* Joining on a dead thread, or on the main thread has no side effects.
	* If the thread is terminated because of an exception, the exception is thrown
	* when this method is called.
	*
	* @param timeout the time to wait for.
	* @return {@code true} if the join returned because of a timeout; otherwise, {@code false}.
	* @throws InvalidOperationException if this is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	*/
	public join(TimeSpan timeout) => (boolean)
	{
		return join(false, timeout);
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates or until it times out.
	* Joining on a dead thread has no side effects. If the thread is terminated because of an exception, 
	* the exception is thrown when this method is called.
	*
	* @param millisecondsTimeout the time to wait for in milliseconds.
	* @return {@code true} if the join returned because of a timeout; otherwise, {@code false}.
	* @throws InvalidOperationException if this is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	*/
	public join(int millisecondsTimeout) => (boolean)
	{
		return join(false, Time.fromMilliSeconds(millisecondsTimeout));
	}
	
	/**
	* Gets whether this thread is running.
	*
	* @return {@code true} if the thread is alive; otherwise, {@code false}.
	*/
	public readonly get isAlive() => (boolean)
	{
		mutex.lock();
		var running = (handle->isValid or id == 0);
		mutex.unlock();
		
		return running;
	}
	
	public readonly get childrenCount() => (int)
	{
		return childrenList->size;
	}
	
	public readonly equal(Thread other) => (boolean) 
	{
		return (this === other);
	}

	public readonly iterator() => (Iterator<Thread>)
	{
		return childrenList.iterator();
	}
	
	public readonly index(int index) => (Thread)
	{
		return childrenList[index];
	}
	
	public destroy
	{
		mutex.free();
	}
	
	private addToParent() => () 
	{
		check(parent):childrenList.add(this);
	}
	
	// TODO: WE NEED TO MAKE SURE THAT THE ACTUAL UNMANAGED POINTER HAS BEEN FREED
	// Do we need to call pthread_exit()? Gotta figure this out.
	private join(boolean finalizing, TimeSpan timeout) => (boolean)
	{
		if(this === System->mainThread) {
			throw InvalidOperationException:create("A thread cannot call join on the main thread.");
		}
		
		if(this === curthread()) {
			throw InvalidOperationException:create("A thread cannot call join on itself.");
		}
		
		boolean timedOut = false;
		boolean flag;
		mutex.lock();
		// finalizing is true when Shadow calls this method. It is false when
		// the user calls it. So, if Shadow called the method, we would only like to join
		// or throw the exception if this thread didn't have join called on it before.
		flag = !finalizing or (finalizing and !joined);
		joined = true;
		
		if(flag and handle->isValid) {
			mutex.unlock();
			
			if(timeout->isPositive) {
				timedOut = signaler.waitForNotify(timeout);				
			} else {
				signaler.waitForNotify();
			}
		} else {
			mutex.unlock(); 
		}

		// we then check here if we have an exception. If we do, we throw it, and
		// it is the job of runnerNative() to take care of the handling.
		if(flag and ex !== null) {
			throw check(ex);
		}
		
		return timedOut;
	}
	
	private sleepNative(TimeSpan timeout) => ()
	{
		signaler.sleep(timeout);
	}
	
	private sleepNative(int milliseconds) => ()
	{
		signaler.sleep(milliseconds);
	}
	
	/**
	* This method is natively executed from the newly spawned thread.
	* !!DO NOT MANUALLY EXECUTE!!
	*/
	private runnerNative() => ()
	{
		try {
			runner.run();
		} catch(Exception e) {
			ex = ThreadException:create(this, e);
		} finally {
			mutex.lock();
			handle.free();
			signaler.notifyAll();
			signaler.free();
			mutex.unlock();
		}
	}
	
	private waitForThreadsNative() => ()
	{
		// we keep two queues, since we would like to throw the exceptions
		// at the very end. First we traverse the children of all the threads,
		// and when there is an exception, we add it to the exceptionQueue.
		var exceptionQueue = ArrayDeque<ThreadException>:create();

		// queue used for traversal of the children
		var threadsQueue = ArrayDeque<Thread>:create();
		threadsQueue.addLast(System->mainThread);

		while(!threadsQueue.isEmpty()) {
			var thread = threadsQueue.removeFirst();

			// we do not wanna perform any operations on the main thread
			// so we skip if id is zero.
			if(thread->id > 0) {
				try {
					// this join does not do anything if the thread had
					// been joined on before.
					thread.join(true, Time->InfiniteTimeout);
				}
				catch (ThreadException e) {
					exceptionQueue.addLast(e);
				}
			}

			// traverse the children and add them to queue.
			foreach(var child in thread:childrenList) {
				threadsQueue.addLast(child);
			}
		}

		// traverse the exceptions, if any and print them as Uncaught Thread Exceptions.
		while(!exceptionQueue.isEmpty()) {
			Console.printErrorLine("Uncaught " # exceptionQueue.removeFirst());
		}
	}
	
	private readonly createNative() => (Thread) 
	{ 
		return Thread:create(NopRunner:create(), "Thread#main"); 
	}
	
	private readonly unlockMutexNative() => () 
	{
		mutex.unlock();
	}
	
	// A Runner which performs no operations.
	private class NopRunner
		is CanRun
	{
		public run() => () {}
	}
}