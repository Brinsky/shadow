import shadow:io@Console;
import shadow:utility;
import shadow:natives;

/**
 * 
 * @author Claude Abounegm
 */
locked class shadow:standard@
	Thread
is CanEqual<Thread>
and CanIndex<int, Thread>
{
	public constant int READY = 0;
	public constant int RUNNING = 1;
	public constant int WAITING = 2;
	public constant int DEAD = 3;
	
	// START: Native methods
	private readonly native spawnThread(Pointer ptr) => (int);
	private readonly native get handleSize() => (int);
	private native get staticNextId() => (int);
	// END: Native methods
	
	// START: Private variables
	nullable Exception ex = null;
	nullable Signaler interruptSignaler = null;

	int state;
	int joined;
	boolean interrupting;

	Pointer handle;
	CanRun runner;
	
	ThreadSafeList<Thread> childrenList;
	CanIterate<Thread> childrenIterate;
	CanIterate<Thread> childrenIterateRecurse;
	
	Mutex mutex;
	Mutex stateMutex;
	
	Signaler signaler;
	Mailbox mailbox;
	// END: Private variables
	
	/// Gets the Shadow-specific thread ID.
	get int id;
	
	/// Gets the main thread, which is the root of all threads.
	public native get main() => (Thread);
	
	/**
	* Gets the {@code Thread} which spawned this thread. The Main thread does not
	* have a parent, and is null.
	*/
	get nullable Thread parent = null;
	
	/**
	* Gets the name of the thread set at spawn time. If no name is set
	* at spawn time, the name takes the format "Thread#{id}".
	*/
	get String name;
	
	/**
	* Gets whether this thread is running.
	*
	* @return {@code true} if the thread is alive; otherwise, {@code false}.
	*/
	public readonly get isAlive() => (boolean)
	{
		return (this->state != DEAD);
	}
	
	private set state(int state) => ()
	{
		stateMutex.lock();
		this:state = state;
		stateMutex.unlock();
	}
	
	/**
	* Gets the state of this thread. It can be one of {@code READY}, {@code RUNNING}, {@code WAITING}, or {@code DEAD}.
	*/
	public readonly get state() => (int)
	{
		stateMutex.lock();
		var state = this:state;
		stateMutex.unlock();
		
		return state;
	}
	
	/// Returns the string representation of the state of this thread.
	public readonly get stateStr() => (String)
	{
		switch(this->state) {
			case (READY)
				return "READY";
			
			case (RUNNING)
				return "RUNNING";
				
			case (WAITING)
				return "WAITING";
				
			case (DEAD)
				return "DEAD";
		}
		
		throw InvalidOperationException:create("Invalid state");
	}
	
	/**
	* Creates a new {@code Thread} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread.
	* 
	* @param runner The instance that contains a run function.
	*/
	public create(CanRun runner)
	{
		this(runner, null);
	}
	
	/**
	* Creates a new {@code Thread} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread, and takes a {@code String} which is the name
	* of the thread for reference purposes.
	* 
	* @param runner The instance that contains a run function.
	* @param name The name of the thread.
	*/
	public create(CanRun runner, nullable String name)
	{
		this:id = this->staticNextId;
		this:childrenList = ThreadSafeList<Thread>:create();
		
		try {
			this:name = check(name);
		} recover {
			this:name = "Thread#" # id;
		}
		
		this:joined = 0;
		this:mutex = Mutex:create();
		this:stateMutex = Mutex:create();
		this:signaler = Signaler:create();
		this:mailbox = Mailbox:create(32);
		this:handle = Pointer:create(this->handleSize);
		this:state = READY;
		this:childrenIterate = TraverseChildren:create(false);
		this:childrenIterateRecurse = TraverseChildren:create(true);
		
		if(this:id == 0) {
			this:runner = runner;
			this:state = RUNNING;
			this:handle.free();
		} else {
			this:runner = copy(runner);
			this:parent = currentThread();
			addToParent();
			
			// spawn the thread and give it the handle
			spawnThread(handle);
			
			// pthreads clean up their resources after the
			// spawned thread exits, so we do not need to
			// call any other functions.
		}
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates.
	* Joining on a dead thread has no side effects. If the thread is terminated because of an exception, 
	* the exception is thrown when this method is called.
	* 
	* @throws InvalidOperationException if this thread is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	* @throws InterruptedException if another thread requested to interrupt this thread.
	*
	* @seeDoc shadow:standard@ThreadException
	*/
	public join() => ()
	{
		join(false, Time->InfiniteTimeout);
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates or until it times out.
	* Joining on a dead thread has no side effects. If the thread is terminated because of an exception, 
	* the exception is thrown when this method is called.
	*
	* @param timeout the time to wait for.
	* @return {@code true} if the join returned because of a timeout; otherwise, {@code false}.
	*
	* @throws InvalidOperationException if this thread is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	* @throws InterruptedException if another thread requested to interrupt this thread.
	*
	* @seeDoc shadow:standard@ThreadException
	*/
	public join(TimeSpan timeout) => (boolean)
	{
		return join(false, timeout);
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates or until it times out.
	* Joining on a dead thread has no side effects. If the thread is terminated because of an exception, 
	* the exception is thrown when this method is called.
	*
	* @param millisecondsTimeout the time to wait for in milliseconds.
	* @return {@code true} if the join returned because of a timeout; otherwise, {@code false}.
	*
	* @throws InvalidOperationException if this thread is the main thread or is the same as the calling thread.
	* @throws ThreadException if the thread terminated due to an exception.
	* @throws InterruptedException if another thread requested to interrupt this thread.
	*
	* @seeDoc shadow:standard@ThreadException
	*/
	public join(int millisecondsTimeout) => (boolean)
	{
		return join(false, Time.fromMilliSeconds(millisecondsTimeout));
	}
	
	private join(boolean finalizing, TimeSpan timeout) => (boolean)
	{
		if(this === main()) {
			throw InvalidOperationException:create("A thread cannot call join on the main thread.");
		}
		
		if(isCurrentThread()) {
			throw InvalidOperationException:create("A thread cannot call join on itself.");
		}
		
		mutex.lock();
		boolean throwException = !finalizing or (finalizing and joined == 0);
		boolean timedOut = false;
		
		joined += 1;
		mutex.unlock();
		
		if(isAlive()) {
			try {
				setCurrentThreadState(WAITING, this:signaler);

				if(timeout->isPositive) {
					timedOut = signaler.waitForNotify(timeout);
				} else {
					signaler.waitForNotify();
				}
			} catch(InterruptedException e) {
				// An interrupted join is not succesful, so we
				// decrease its count.
				mutex.lock();
				joined -= 1;
				mutex.unlock();

				throw e;
			} finally {
				setCurrentThreadState(RUNNING, null);
			}
		}

		// we then check here if we have an exception. If we do, we throw it, and
		// it is the job of runnerNative() to take care of the handling.
		if(throwException and ex !== null) {
			throw check(ex);
		}

		return timedOut;
	}
	
	/**
	* Tries to interrupt this thread. 
	*/
	public interrupt() => ()
	{
		if(!isAlive()) {
			return;
		}
		
		mutex.lock();
		interrupting = true;
		try {
			if(this->state == WAITING) {
				interrupting = false;
				check(interruptSignaler).interrupt(this);
			}
			
			// When the InterruptedException is raised, the runnerNative() catches that exception. 
			// Just like any other exception, `ex` is set to that exception, and runnerNative() then cleans
			// up the resources it used. It calls signaler.free(), which in turn causes Signaler to release
			// all the threads it is holding through a waitForNotify(). Since all threads get woken up after
			// the exception `ex` has been set, all the joined thread also get an InterruptedException correctly.
		} finally {
			mutex.unlock();
		}
	}
	
	/**
	* 
	*/
	public get shouldInterrupt() => (boolean)
	{
		if(!isCurrentThread()) {
			throw InvalidOperationException:create("shouldInterrupt() should only be called from the current thread.");
		}
		
		mutex.lock();
		try {
			if(interrupting) {
				interrupting = false;
				return true;
			}
			
			return false;
		} finally {
			mutex.unlock();
		}
	}

	/**
	* @throws InterruptedException
	* @seeDoc shadow:natives@Signaler
	*/
	private sleepNative(TimeSpan timeout) => ()
	{
		setCurrentThreadState(WAITING, signaler);
		signaler.sleep(timeout);
		setCurrentThreadState(RUNNING, null);
	}
	
	/**
	* @throws InterruptedException
	* @seeDoc shadow:natives@Signaler
	*/
	private sleepNative(int millisecondsTimeout) => ()
	{
		sleepNative(Time.fromMilliSeconds(millisecondsTimeout));
	}

	/**
	* @throws InterruptedException
	* @seeDoc shadow:standard@Mailbox
	*/
	private receiveNative(Class expectedType) => (Object, Thread)
	{	
		setCurrentThreadState(WAITING, mailbox->signaler);
		var retVal = mailbox.dequeue(expectedType);
		setCurrentThreadState(RUNNING, null);
		
		return retVal;
	}
	
	/**
	* @throws InterruptedException
	* @throws IllegalArgumentException 
	* @seeDoc shadow:standard@Mailbox
	*/
	private receiveNative(Class expectedType, Thread from) => (Object)
	{
		if(from === this) {
			throw IllegalArgumentException:create("The {from} thread cannot be the same as this thread.");
		}
		
		setCurrentThreadState(WAITING, mailbox->signaler);
		var retVal = mailbox.dequeue(expectedType, from);
		setCurrentThreadState(RUNNING, null);
		
		return retVal;
	}
	
	/**
	* @throws InterruptedException
	* @seeDoc shadow:standard@Mailbox
	*/
	private sendNative(Object data) => ()
	{
		if(!isAlive()) {
			throw InvalidOperationException:create("Cannot send a message to a dead thread.");
		}
		
		if(isCurrentThread()) {
			throw InvalidOperationException:create("Cannot send a message to self.");
		}
		
		setCurrentThreadState(WAITING, mailbox->signaler);
		mailbox.enqueue(data);
		setCurrentThreadState(RUNNING, null);
	}
	
	/**
	* This method is natively executed from the newly spawned thread.
	* !!DO NOT MANUALLY EXECUTE!!
	*/
	private runnerNative() => ()
	{
		try {
			this->state = RUNNING;
			runner.run();
		} catch(Exception e) {
			ex = ThreadException:create(this, e);
		} finally {
			this->state = DEAD;

			mutex.lock();
			handle.free();
			signaler.free();
			mutex.unlock();
		}
	}
	
	// THIS METHOD GETS CALLED WITH A NULL `this` POINTER, DO NOT USE
	// `this` or any instance methods on this thread.
	private waitForThreadsNative() => ()
	{
		if(currentThread() !== main()) {
			throw Exception:create("waitForThreadsNative() can only be called on the main thread.");
		}
		
		var exceptionQueue = ArrayDeque<ThreadException>:create();
		
		foreach(var thread in currentThread().children(true)) {
			try {
				thread.join(true, Time->InfiniteTimeout);
				// the `true` causes this thread to wait for the other thread
				// to terminate and it will only throw an exception if the target 
				// thread had not been joined on before.
			} catch (ThreadException e) {
				exceptionQueue.addLast(e);
			}
		}

		// traverse the exceptions, if any and print them as Uncaught Thread Exceptions.
		while(!exceptionQueue.isEmpty()) {
			Console.printErrorLine("Uncaught " # exceptionQueue.removeFirst());
		}
	}
	
	/// Gets whether this thread is the current executing thread.
	public readonly get isCurrentThread() => (boolean)
	{
		return (currentThread() === this);
	}
	
	/**
	* Gets the number of children the thread currently has. This method should be used with caution
	* even though it is thread safe. Since a thread can spawn threads at any time, this count can
	* drastically change between each call.
	*/
	public readonly get childrenCount() => (int)
	{
		return childrenList->size;
	}
	
	/**
	* Gets the n-th child that this thread spawned. This is NOT the Shadow-specific {@code Thread->id}.
	* If the thread has four children, this method could be used to retrieve the n-th child, for example
	* the third child (thus, its index is 2).
	* @param index the index of the child in the range [0, childrenCount).
	*/
	public readonly childAt(int index) => (Thread)
	{
		return childrenList[index];
	}
	
	/**
	* Gets the n-th child that this thread spawned. This is NOT the Shadow-specific {@code Thread->id}.
	* If the thread has four children, this method could be used to retrieve the n-th child, for example
	* the third child (thus, its index is 2).
	* @param index the index of the child in the range [0, childrenCount).
	*/
	public readonly index(int index) => (Thread)
	{
		return childAt(index);
	}
	
	/**
	* Gets the children of this thread. If {@code recurse} is true, this method
	* will also traverse the children of the children recursively, until all children
	* have been visited.
	* Care should be taken using this function, as enumerating can throw an exception
	* if the children have changed.
	*
	* @return {@code CanIterate<Thread>} An iterable class to be used with a foreach loop.
	*/
	public readonly children(boolean recurse) => (CanIterate<Thread>)
	{
		if(recurse) {
			return childrenIterateRecurse;
		}
		
		return childrenIterate;
	}
	
	/**
	* Gets the children of this thread. This method only enumerates through the first generation
	* children of this thread. i.e. only the thread that this thread spawned.
	*
	* @return {@code CanIterate<Thread>} An iterable class to be used with a foreach loop.
	*/
	public readonly children() => (CanIterate<Thread>)
	{
		return children(false);
	}
	
	public readonly equal(Thread other) => (boolean) 
	{
		return (this === other);
	}
	
	public readonly toString() => (String)
	{
		return name # ": (" # this->stateStr # ")";
	}
	
	private addToParent() => () 
	{
		check(parent):childrenList.add(this);
	}
	
	private setCurrentThreadState(int state, nullable Signaler interruptSignaler) => ()
	{
		var thread = currentThread();
		if(thread->shouldInterrupt) {
			throw InterruptedException:create();
		}
		
		thread:stateMutex.lock();
		thread:state = state;
		thread:interruptSignaler = interruptSignaler;
		thread:stateMutex.unlock();
	}
	
	private readonly currentThread() => (Thread)
	{
		return CurrentThread->instance;
	}
	
	private readonly createNative() => (Thread) 
	{ 
		return Thread:create(NopRunner:create(), "Thread#main"); 
	}
	
	private readonly instance() => (Thread)
	{
		return this;
	}
	
	// A Runner which performs no operations.
	private class NopRunner
		is CanRun
	{
		public run() => () {}
	}
	
	private class TraverseChildren is CanIterate<Thread>
	{
		boolean recurse;
		
		public create(boolean recurse)
		{
			this:recurse = recurse;
		}
		
		public readonly iterator() => (Iterator<Thread>)
		{
			if(recurse) {
				return ChildrenIterator:create();
			}

			return instance():childrenList.iterator();
		}
		
		private class ChildrenIterator is Iterator<Thread>
		{
			ArrayDeque<Thread> threadsQueue;
			Thread current = instance();
			
			public create()
			{
				threadsQueue = ArrayDeque<Thread>:create();
			}
			
			public readonly hasNext() => (boolean)
			{
				return (!threadsQueue.isEmpty() or current->childrenCount > 0);
			}
			
			public next() => (Thread)
			{
				addChildrenToQueue(current);
				
				if(threadsQueue.isEmpty()) {
					throw NoSuchElementException:create();
				}
				
				current = threadsQueue.removeFirst();
				return current;
			}
			
			private addChildrenToQueue(Thread parent) => ()
			{
				foreach(var child in parent.children()) {
					threadsQueue.addLast(child);
				}
			}
		}
	}
}