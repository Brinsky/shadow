import shadow:io@Console;

immutable class shadow:standard@BigInteger
implements Number<BigInteger>
{
	uint[] data;
	boolean negative;
	
	constant ulong BASE = 4294967296uL;
	
	public equal(BigInteger other) => (boolean)
	{
		return compare(other) == 0;
	}
	
	public compare(BigInteger other) => (int)
	{
		if( negative )
		{
			if( other:negative )
				return -unsignedCompare( data, other:data );
			else
				return -1;		
		}
		else
		{
			if( other:negative )
				return 1;
			else
				return unsignedCompare( data, other:data );
		}	
	}
	
	public add(BigInteger other) => (BigInteger)
	{
		if( negative == other:negative )
			return BigInteger:create( unsignedAdd(data, other:data), negative );
			
		//opposite signs
		int comparison = unsignedCompare(data, other:data);
		if( comparison > 0 )
			return BigInteger:create( unsignedSubtract(data, other:data), negative );
		else if( comparison < 0 )
			return BigInteger:create( unsignedSubtract(other:data, data), other:negative);
		else
			return BigInteger:create( 0 );		
	}
	
	public subtract(BigInteger other) => (BigInteger)
	{
		if( negative != other:negative )
			return BigInteger:create( unsignedAdd(data, other:data), negative );
			
		//same signs
		int comparison = unsignedCompare(data, other:data);
		if( comparison > 0 )
			return BigInteger:create( unsignedSubtract(data, other:data), negative );
		else if( comparison < 0 )
			return BigInteger:create( unsignedSubtract(other:data, data), !other:negative);
		else
			return BigInteger:create( 0 );		
	}
	
	public multiply(BigInteger other) => (BigInteger)
	{	
		return BigInteger:create( unsignedMultiply( data, other:data), negative xor other:negative );	
	}
	
	private unsignedMultiply(readonly uint[] first, readonly uint[] second) => (readonly uint[])
	{
		uint[] result = uint:create[first->size + second->size];
		for( int i = 0; i < first->size; i += 1 )
		{		
			if( first[i] > 0u )
			{
				ulong carry = 0uL;
				ulong product = 0uL;
			
				for( int j = 0; j < second->size; j += 1 )
				{
					product = cast<ulong>(first[i])*second[j] + result[i + j] + carry ;
					carry = product / BASE;
					result[i + j] = cast<uint>(product % BASE);
				}
				
				//never happens the last time
				if( carry > 0L )
					result[i + second->size] = cast<uint>(carry);
			}															
		}
		
		return removeZeroes(result);
	}
	
	public divide(BigInteger other) => (BigInteger)
	{
		int comparison = unsignedCompare(data, other:data);
		if( comparison < 0 )
			return BigInteger:create( 0 );
		
		if( comparison == 0 )
			return BigInteger:create( (negative xor other:negative ? -1 , 1) );
			
		(readonly uint[] quotient, ) = unsignedDivide( data, other:data );
					
		return BigInteger:create( quotient, negative xor other:negative );
	}
	
	public modulus(BigInteger other) => (BigInteger)
	{
		int comparison = unsignedCompare(data, other:data);
		if( comparison < 0 )
			return this;
		
		if( comparison == 0 )
			return BigInteger:create( 0 );
			
		(, readonly uint[] remainder) = unsignedDivide( data, other:data );
		
		if( remainder->size == 1 and remainder[0] == 0u )
			return BigInteger:create( 0 );
		
		return BigInteger:create( remainder, negative );
	}
	
	private leadingZeroes(uint value) => (uint)
	{		
		uint zeroes = 0u;
		if (value <= 0x0000FFFF) {zeroes += 16u; value = value << 16;}
		if (value <= 0x00FFFFFF) {zeroes += 8u; value = value << 8;}
		if (value <= 0x0FFFFFFF) {zeroes += 4u; value = value << 4;}
		if (value <= 0x3FFFFFFF) {zeroes += 2u; value = value << 2;}
		if (value <= 0x7FFFFFFF) {zeroes += 1u;}	   
		return zeroes;
	}
	
	private unsignedLeftShift(readonly uint[] data, uint amount) => (readonly uint[])
	{	
		int jump = cast<int>(amount/32u);
		int shift = cast<int>(amount%32u);
		uint[] result = uint:create[data->size + jump + 1];

		result[result->size - 1] = data[result->size - 1 - jump - 1] >> (32 - shift);
		for( int i = jump + 1; i < result->size - 1; i += 1 )
			result[i] = (data[i - jump] << shift) | (data[i - jump - 1] >> (32 - shift));
		result[jump] = data[0] << shift;
		for( int i = 0; i < jump; i += 1 )
			result[i] = 0u;
			
		return removeZeroes( result );
	}
	
	private unsignedRightShift(readonly uint[] data, uint amount) => (readonly uint[])
	{	
		int jump = cast<int>(amount/32u);
		int shift = cast<int>(amount%32u);
		uint[] result = uint:create[(data->size - jump).max(1)];
		for( int i = 0; i < result->size - 1; i += 1 )
			result[i] = (data[i + jump] >> shift) | (data[i + jump + 1] << (32 - shift));
		if( data->size > jump )
			result[result->size - 1] = data[data->size - 1] >> shift;
		else
			result[result->size - 1] = 0u;
			
		return removeZeroes( result );
	}
	
	//unsignedDivide assumes that first is no smaller than second
	private unsignedDivide(readonly uint[] first, readonly uint[] second) => (readonly uint[] quotient, readonly uint[] remainder)
	{
		uint[] quotient = uint:create[first->size - second->size + 1];
		int comparison;
		ulong value;
		
		if( isZero( second ) )
			throw IllegalArgumentException:create("Division by zero");
			
		if( second->size == 1 ) //single digit divisor is handled specially
		{
			value = 0uL;
			ulong divisor = second[0];
			for( int i = first->size - 1; i >= 0; i -= 1 )
			{
				quotient[i] = cast<uint>((value*BASE + first[i])/divisor);
				value = (value*BASE	 + first[i]) - quotient[i]*divisor;			
			}
			
			uint[] remainder = uint:create[1];
			remainder[0] = cast<uint>(value);
			
			return (removeZeroes(quotient), remainder );		
		}
		
		uint zeroes = leadingZeroes(second[second->size - 1]);
		readonly uint[] normalizedDivisor = 	unsignedLeftShift( second, zeroes );
		readonly uint[] normalizedData = 	unsignedLeftShift( first, zeroes );
		ulong quotientEstimate; 
		ulong remainderEstimate;		
		long test;
		long carry;
		
		for( int i = first->size - second->size; i >= 0; i -= 1 )
		{
			value = normalizedData[i + second->size]*BASE + normalizedData[i + second->size - 1];
			quotientEstimate = value/normalizedDivisor[second->size - 1]; 			
			remainderEstimate = value - quotientEstimate*normalizedDivisor[second->size - 1];
			
			while( quotientEstimate >= BASE or
				   quotientEstimate*normalizedDivisor[second->size - 2] > BASE*remainderEstimate + normalizedData[i + second->size - 2] )
			{
				quotientEstimate  -= 1uL;
				remainderEstimate += normalizedDivisor[second->size - 1];
				
				if( remainderEstimate >= BASE )
					break;			
			}
			
			carry = 0L;
			for( int j = 0; j < second->size; j += 1 )
			{
				value = quotientEstimate * normalizedDivisor[j];
				test = cast<long>(normalizedData[i + j]) - carry - cast<long>(value & 0xFFFFFFFFuL);  
				normalizedData[i + j] = cast<uint>(test);
				carry = cast<long>(value >> 32) - (test >> 32);
			}
			test = cast<long>(normalizedData[i + second->size]) - carry;  
			normalizedData[i + second->size] = cast<uint>(test);

			quotient[i] = cast<uint>(quotientEstimate);
			
			if( test < 0L )
			{						
				quotient[i] -= 1u;
				carry = 0L;
				for( int j = 0; j < second->size; j += 1 )
				{
					test = carry + normalizedData[i + j] + normalizedDivisor[j];
					normalizedData[i + j] = cast<uint>(test);
					carry = test / cast<long>(BASE);				
				}
				
				normalizedData[i + second->size] += cast<uint>(carry);
			}
		}
		return (removeZeroes(quotient), unsignedRightShift(normalizedData, zeroes));
	}
	
	public create(int value)
	{
		uint[] temp = uint:create[1]; 
		temp[0] = value.abs();		
		data = freeze(temp);
		negative = value < 0;			
	}
	
	public create(ulong magnitude)
	{	
		uint[] temp;
		
		if( magnitude >= BASE )
		{
			temp = uint:create[2];
			temp[1] = cast<uint>(magnitude / BASE); 
			temp[0] = cast<uint>(magnitude % BASE);					
		}
		else
		{
			temp = uint:create[1];			 
			temp[0] = cast<uint>(magnitude);		
		}		
		
		data = freeze(temp);
	}
	
	public create(long value)
	{
		ulong magnitude = value.abs();
		uint[] temp;
		if( magnitude >= BASE )
		{
			temp = uint:create[2];
			temp[1] = cast<uint>(magnitude / BASE); 
			temp[0] = cast<uint>(magnitude % BASE);					
		}
		else
		{
			temp = uint:create[1];			 
			temp[0] = cast<uint>(magnitude);		
		}
		
		data = freeze(temp);
		negative = value < 0L;		
	}
	
	public create(String value)
	{
		this(value, 10u);	
	}
	
	
	public create(String value, uint base)
	{	
		if( base < 2u or base > 36u )
			throw IllegalArgumentException:create("Invalid base: " # base);
	
		ulong initial = 0uL;		
		int i = 0;
		if( value->size > 0 and value[i] == '-' )
		{
			negative = true;
			i += 1;
		}
		
		//try to fit into a single "digit"				
		while( i < value->size and initial < BASE )
		{
			initial *= base;
			initial += digitValue( value[i], base );
			i += 1;					
		}
		
		uint[] values;
		readonly uint[] holder;  //necessary because of readonly issues
	
		if( initial < BASE )
		{
			values = uint:create[1];
			values[0] = cast<uint>(initial);
			holder = values;
		}
		else
		{
			//undo last operation
			i -= 1;
			initial -= digitValue( value[i], base );
			initial /= base;
			values = uint:create[1];
			values[0] = cast<uint>(initial);
			uint[] baseRepresentation = uint:create[1];
			baseRepresentation[0] = base;
			uint[] digitRepresentation = uint:create[1];
			
			holder = values;		
			
			while( i < value->size )
			{
				holder = unsignedMultiply( holder, baseRepresentation );
				digitRepresentation[0] = digitValue( value[i], base );				
				holder = unsignedAdd( holder, digitRepresentation );
				i += 1;			
			}
		}
		
		data = freeze(holder);	
	}
	
	private digitValue(byte letter, uint base) => (uint)
	{
		uint value = 0u;
	
		if( letter >= '0' and letter <= '9' )
			value = cast<uint>(letter - '0');
		else if( letter >= 'A' and letter <= 'Z' )
			value = cast<uint>(letter - 'A' + 10);
		else if( letter >= 'a' and letter <= 'z' )
			value = cast<uint>(letter - 'a' + 10);
		else
			throw IllegalArgumentException:create("Invalid digit: " # letter);
			
		if( value >= base )
			throw IllegalArgumentException:create("Invalid digit: " # letter);
			
		return value;
	}
	
	private unsignedCompare(readonly uint[] data, readonly uint[] other) => (int)
	{
		if( data->size < other->size )
			return -1;
		
		if( data->size > other->size )
			return 1;
			
		for( int i = data->size - 1; i >= 0; i -= 1 )
		{
			if( data[i] < other[i] )
				return -1;
			else if( data[i] > other[i] )
				return 1;
		}
		
		return 0;
	}
	
	private unsignedAdd(readonly uint[] first, readonly uint[] second) => (readonly uint[])
	{
		uint[] result = uint:create[first->size.max(second->size) + 1];
		ulong carry = 0uL;
		ulong sum = 0uL;
		for( int i = 0; i < result->size; i += 1 )
		{
			sum = carry;
			if( i < first->size )
				sum += first[i];
			if( i < second->size )
				sum += second[i];
			carry = sum / BASE;
			result[i] = cast<uint>(sum % BASE);								
		}
		
		return removeZeroes( result );
	}
	
	//unsigned subtract assumes that second is never larger than first
	private unsignedSubtract(readonly uint[] first, readonly uint[] second) => (readonly uint[] )
	{
		uint[] result = uint:create[first->size];
		ulong borrow = 0uL;
		ulong subtract = 0uL;
		for( int i = 0; i < result->size; i += 1 )
		{
			subtract = borrow;			
			if( i < second->size )
				subtract += second[i];
			if( subtract <= first[i] )
			{
				result[i] = cast<uint>(first[i] - subtract);
				borrow = 0uL;
			}
			else
			{
				result[i] = cast<uint>(BASE + first[i] - subtract);
				borrow = 1uL;
			}				
		}
		
		return removeZeroes( result );
	}
	
	
	private create( readonly uint[] data, boolean negative )
	{
		this:data = freeze(data);
		this:negative = negative;	
	}
	
	
	private removeZeroes( readonly uint[] values ) => ( readonly uint[] )
	{
		int zeroes = 0;
		int index = values->size - 1; 
		while( index >= 0 and values[index] == 0 )
		{
			zeroes += 1;
			index -= 1;
		}
			
		if( zeroes == values->size )
			zeroes -= 1;
			
		if( zeroes > 0 )
			return cast<uint[]>(values.subarray( 0, values->size - zeroes ));
		
		return values;
	}
	
	public toString() => (String)
	{
		return toString(10u);	
	}
	
	private print(readonly uint[] values, Console out) => ()
	{
		out.print(values->size # ": ["); 
		for( int i = 0; i < values->size; i += 1 )
			out.print(values[i] # " ");	
		out.printLine("]");	
	}
	
	public toString(uint base) => (String)
	{
		if( base < 2u or base > 36u )
			throw IllegalArgumentException:create("Invalid base: " # base);
		
		if( isZero(data) )
			return "0";
			
		MutableString buffer = MutableString:create();
				
		if( negative )
			buffer.append("-");			
			
		if( data->size == 1 )
			return buffer.append( data[0].toString(base) ).toString();
			
		BigInteger divisor = BigInteger:create(base.toInt());
		
		readonly uint[] number = data;
		readonly uint[] quotient;		
		readonly uint[] remainder;		
			
		Console out = Console:instance;
				
		do
		{
			(quotient, remainder) = unsignedDivide( number, divisor:data );			
		
			uint digit = remainder[0]; 
			
			if( digit >= 10u ) //A through Z
				buffer.append(cast<byte>(digit + 'A') - 10y);
			else
				buffer.append(cast<byte>(digit + '0'));
				
			number = quotient;				
		} while( !isZero( number ) );
				
		return buffer.reverse().toString();
	}
	
	public isZero(readonly uint[] data) => (boolean)
	{
		return data->size == 1 and data[0] == 0u;
	}
	
	immutable public toByte() => (byte)
	{
		return cast<byte>(toInt());
	}
	
	public toUByte() => (ubyte)
	{
		return cast<ubyte>(data[0]);
	}
	
	public toShort() => (short)
	{
		return cast<short>(toInt());
	}
	
	public toUShort() => (ushort)
	{
		return cast<ushort>(data[0]);
	}
	
	public toInt() => (int)
	{
		return (negative ? -1 , 1 ) * cast<int>(data[0]);
	}
	
	public toUInt() => (uint)
	{
		return data[0];
	}
	
	public toLong() => (long)
	{
		return (negative ? -1L , 1L ) * cast<long>(toULong());
	}
	
	public toULong() => (ulong)
	{
		ulong value = data[0];
		if( data->size > 1 )
			value += data[1] * BASE;
		return value;	
	}
	
	public toCode() => (code)
	{
		return cast<code>(data[0]);	
	}	
	
	public toFloat() => (float)
	{
		return cast<float>( toDouble() );
	}
		
	public toDouble() => (double)
	{
		double total = 0.0;
		
		for( int i = data->size - 1; i >= 0; i -= 1 )
		{
			total *= BASE;
			total += data[i];		
		}
		
		return negative ? -total , total; 
	}
	
	public abs() => (BigInteger)
	{
		if( negative )
			return BigInteger:create(data, false);
			
		return this;
	}
	
	public negate() => (BigInteger)
	{
		return BigInteger:create(data, !negative);	
	}
	
	public min(BigInteger other) => (BigInteger)
	{
		if( compare(other) < 0 )
			return this;
		else
			return other;	
	}
	
	public max(BigInteger other) => (BigInteger)
	{
		if( compare(other) > 0 )
			return this;
		else
			return other;	
	}
	
	public bitShiftLeft(int amount) => (BigInteger)
	{
		uint abs = amount.abs();
		if( amount > 0 )
			return BigInteger:create( unsignedLeftShift(data, abs), negative );
		else if( amount < 0 )
			return BigInteger:create( unsignedRightShift(data, abs), negative );
		else
			return this;			
	}
	
	public bitShiftRight(int amount) => (BigInteger)
	{
		uint abs = amount.abs();
		if( amount > 0 )
			return BigInteger:create( unsignedRightShift(data, abs), negative );
		else if( amount < 0 )
			return BigInteger:create( unsignedLeftShift(data, abs), negative );
		else
			return this;			
	}
	
	public hash() => (int)
	{
		int value = 0;
		
		for( int i = 0; i < data->size; i += 1 )
			value = 31*value + cast<int>(data[i]);
			
		return negative ? -value, value;	
	}
	
	public power(BigInteger exponent) => (BigInteger)
	{
		if( exponent:negative )
			throw IllegalArgumentException:create("Negative exponent: " # exponent);
		
		if( isZero( exponent:data ) )
			return BigInteger:create(1);
			
		if( exponent:data->size == 1 and exponent:data[0] == 1u )
			return this;
		
		BigInteger halfExponent = exponent.bitShiftRight(1);	
		BigInteger halfPower = power( halfExponent );
		BigInteger fullPower = halfPower.multiply(halfPower);
		
		if( exponent:data[0] % 2u == 1u )
		  	fullPower = fullPower.multiply( this );
		  	
		return fullPower; 
	}
	
	public modulusPower(BigInteger exponent, BigInteger m) => (BigInteger)
	{
		if( m:negative or isZero(m:data) )
			throw IllegalArgumentException:create("Negative modulus: " # m);
			
		if( exponent:negative )		
			exponent = m.subtract( exponent );
			
		if( exponent.compare( m ) >= 0 )
			exponent = exponent.modulus(m);
		
		if( isZero( exponent:data ) )
			return BigInteger:create(1);
			
		if( exponent:data->size == 1 and exponent:data[0] == 1u )
			return this.modulus(m);
		
		BigInteger halfExponent = exponent.bitShiftRight(1);	
		BigInteger halfPower = modulusPower( halfExponent, m );
		BigInteger fullPower = halfPower.multiply(halfPower);
		
		if( exponent:data[0] % 2u == 1u )
		  	fullPower = fullPower.multiply( this );
		  	
		return fullPower.modulus(m); 
	}
	
	
	
}