import shadow:io@Console;
import shadow:utility@LinkedList;

locked class shadow:standard@
	ThreadWorker
{	
	// START: Native methods
	private createNative() => (ThreadWorker) { return ThreadWorker:create(); }
	private unlockMutexNative() => () { check(mutex).unlock(); }

	private native createHandle() => (nullable Object);
	private native freeHandle(nullable Object handle) => ();

	private native spawnThread(nullable Object handle) => (int);
	private native joinThread(Object handle) => (int);
	
	private native get staticNextId() => (int);
	// END: Native methods
	
	/**
	* This is the os-specific handle, and cannot be dereferenced in Shadow.
	* However, it is not null when the thread is running, and is null when
	* the thread is dead. So, it can be evaluated to see whether a thread is running.
	*/
	nullable Object handle = null;
	nullable Exception ex = null;
	nullable Mutex mutex = null;
	CanRun runner;
	
	/**
	* Gets the Shadow-specific thread ID.
	*/
	get int id;
	
	/**
	* Gets the {@code ThreadWorker} which spawned this thread.
	*/
	get nullable ThreadWorker parent;
	
	/**
	* Gets the name of the thread set at spawn time. If no name is set
	* at spawn time, the name takes the format "Thread#{id}".
	*/
	get String name;
	
	/**
	* Gets whether this thread is running.
	*/
	public get isRunning() => (boolean)
	{
		return (name == "main" or handle !== null);
	}
	
	/**
	* This method is only used to create a thread for the main
	* thread. The main thread needs an instance as well.
	*/
	private create()
	{
		this:runner = NopRunner:create();
		this:id = this->staticNextId;
		this:parent = null;
		this:name = "Thread#main";
	}
	
	/**
	* Creates a new {@code ThreadWorker} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread.
	* 
	* @param runner The instance that contains a run function.
	*/
	public create(CanRun runner)
	{
		this(runner, null);
	}
	
	/**
	* Creates a new {@code ThreadWorker} object which takes on a {@code CanRun}
	* instance, containing a run function that is going to be ran
	* on a different thread, and takes a {@code String} which is the name
	* of the thread for reference purposes.
	* 
	* @param runner The instance that contains a run function.
	* @param name The name of the thread.
	*/
	public create(CanRun runner, nullable String name)
	{
		this:runner = copy(runner);
		this:id = this->staticNextId;
		this:parent = Thread->current;
		
		try {
			this:name = check(name);
		}
		recover {
			this:name = "Thread#" # id;
		}
		
		this:mutex = Mutex:create();
		
		// we allocate the handle first
		this:handle = createHandle();
		// spawn the thread and give it the handle
		spawnThread(handle);
	}
	
	/**
	* Suspends the execution of the calling thread until this thread terminates. 
	* Joining on a dead thread, or on the main thread has no side effects.
	* 
	* @throws {@code InvalidOperationException} if this is the main thread or is the same as the calling thread.
	* @throws {@code Exception} the exception that caused this thread to terminate.
	*/
	public join() => ()
	{
		if(this === Thread->main)
			throw InvalidOperationException:create("A thread cannot call join on the main thread.");
		if(this === Thread->current)
			throw InvalidOperationException:create("A thread cannot call join on itself.");
		
		Mutex mutex = check(mutex);
		mutex.lock();
		try {
			Object handle = check(handle);
			
			// joinThread() unlocks the mutex before waiting for the thread to terminate.
			joinThread(handle);
		} recover { mutex.unlock(); }

		try {
			throw check(ex);
		} recover {}
	}
	
	/**
	* This method is natively executed from the newly spawned thread.
	* DO NOT MANUALLY EXECUTE.
	*/
	private runnerNative() => ()
	{
		try {
			runner.run();
		} catch(Exception e) {
			ex = e;
		} finally {
			Mutex mutex = check(mutex);
			mutex.lock(); {
				// natively free of the handle
				freeHandle(handle);
				
				// the native is invalid now, set it to null
				handle = null;
			} mutex.unlock();
		}
	}
	
	locked class ThreadData is CanEqual<ThreadData> {
		get Object data;
		get ThreadWorker sender;
		
		public create(Object data, ThreadWorker sender) {
			this:data = data;
			this:sender = sender;
		}
		
		public readonly equal(ThreadData other) => (boolean) {
			return (data === other:data and sender === other:sender);
		}
	}
	
	// A Runner which performs no operations.
	private class NopRunner
		is CanRun
	{
		public run() => () {}
	}
}