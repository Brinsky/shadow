/**
 * Class {@code Mutex} is an implementation of Mutual-Exclusion in Shadow. This implementation
 * allows nested locks, which can be useful in some scenarios, such as with recursive methods.
 *
 * @author Claude Abounegm
 *
 * @seeDoc shadow:test@MutexTest
 *
 * @seeDoc shadow:natives@Signaler
 * @seeDoc shadow:standard@Thread
 * @seeDoc shadow:standard@Mailbox
 */
locked immutable class shadow:natives@
	Mutex
{
	private readonly extern $setOwner(immutable Owner owner, nullable Thread o) => ();
	
	private readonly extern __ShadowMutex_Initialize() => (ShadowPointer);
	private readonly extern __ShadowMutex_Destroy(ShadowPointer ptr) => (boolean);
	private readonly extern __ShadowMutex_Lock(ShadowPointer ptr) => (boolean);
	private readonly extern __ShadowMutex_Unlock(ShadowPointer ptr) => (boolean);
	
	get ShadowPointer ptr;
	immutable Owner owner;
	boolean allowRecursiveLocks;
	
	/**
	* Initializes a new {@code Mutex} which does not allow nested locks.
	*/
	public create()
	{
		this(false);
	}
	
	/**
	* Initializes a new {@code Mutex} which can allow nested locks.
	* @param allowRecursiveLocks whether the Mutex should allow mutliple nested locks.
	*/
	public create(boolean allowRecursiveLocks)
	{
		ptr = __ShadowMutex_Initialize();
		if(!ptr->isValid) {
			throw MutexException:create("An error occured while initializing the Mutex.");
		}
		
		owner = freeze(Owner:create());
		this:allowRecursiveLocks = allowRecursiveLocks;
	}
	
	/**
	* Attempts to lock this Mutex. If the Mutex is already owned by another thread, this method
	* blocks until the Mutex becomes available. If the current thread already owns this mutex and
	* recursive locks are allowed, this method will simply increase the counter. The mutex should be unlocked
	* the same number of times it was locked.
	*
	* @throws MutexException if recursive locks are not allowed and the mutex is already owned by this thread.
	* @throws FreedResourceException if the Mutex has been freed.
	*/
	public lock() => ()
	{
		ptr.assertValid("Mutex");
		
		if(!owner.isCurrentThread()) {
			__ShadowMutex_Lock(ptr);
		} else if(!allowRecursiveLocks) {
			throw MutexException:create("This thread already owns this mutex.");
		}

		$setOwner(owner, CurrentThread->instance);
	}
	
	/**
	* Attempts to unlock this Mutex. If the Mutex has been locked mutliple times by this thread,
	* this method simply decreases the counter. This Mutex is unlocked when an corresponding unlock has been called
	* for each lock. i.e. If {@code lock()} was called twice, {@code unlock()} will unlock the mutex on
	* its second call.
	*
	* @throws MutexException if this Mutex is not owned by this thread.
	* @throws FreedResourceException if the Mutex has been freed.
	*/
	public unlock() => ()
	{
		ptr.assertValid("Mutex");

		if(!owner.isCurrentThread()) {
			throw MutexException:create("This mutex is not owned by '" # CurrentThread->name # "' and cannot be unlocked.");
		}
		
		$setOwner(owner, null);
		if(!owner.isCurrentThread()) {
			__ShadowMutex_Unlock(ptr);
		}
	}
	
	/**
	* Frees the resources allocated by this {@code Mutex}.
	*
	* @throws MutexException if the Mutex is locked by another thread.
	*/
	public free() => ()
	{
		if(ptr->isValid) {
			if(owner.isCurrentThread()) {
				unlock();
			} else if(owner.isLocked()) {
				throw MutexException:create("Cannot free a locked Mutex.");
			}
			
			__ShadowMutex_Destroy(ptr);
			ptr.free();
		}
	}
	
	// We would like to have the Mutex as immutable to allow multithreaded
	// code to be immutable too. This Owner class allows us to freeze it,
	// then call the actual non-readonly method through native code.
	private class Owner
	{
		nullable Thread owner;
		int counter;
		
		public create()
		{
			owner = null;
			counter = 0;
		}
		
		public readonly isLocked() => (boolean)
		{
			return (owner !== null);
		}
		
		public readonly isCurrentThread() => (boolean)
		{
			return CurrentThread.isEqualTo(owner);
		}
		
		private $[Mutex] setOwner(nullable Thread o) => ()
		{
			if(o !== null) {
				if(counter == 0) {
					owner = o;
				} else if(owner !== o) { // should never reach this, unless (take/giveUp)Ownership were incorrectly used.
					throw MutexException:create("Cannot change Mutex owners. The owner of this mutex needs to first give up ownership of this mutex.");
				}
				
				counter += 1;
			} else {
				if(counter == 1) {
					owner = null;
				}
				
				counter -= 1;
			}
		}
	}
}