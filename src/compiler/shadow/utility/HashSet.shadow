class shadow.utility@HashSet<V is CanHash and CanEqual<V>> implements Set<V>
{
	get int size = 0;
	Node[] table = Node:create[256];
	float load;
	int modifications = 0;

	private class Node
	{
		immutable get int hash;
		get nullable V value;
		get set nullable Node next = null;
		
		public readonly get next() => (nullable Node)
		{
			return this:next;
		}
		
		public readonly get value() => (nullable V)
		{
			return this:value;
		}

		public create( int initialHash, V initialValue )
		{
			hash = initialHash;
			value = initialValue;
		}
		public create( int initialHash, V initialValue, nullable Node after )
		{
			hash = initialHash;
			value = initialValue;
			next = after;
		}
	}

	private immutable hash( V value ) => ( int index, int hash )
	{
		uint temp = value->hash;
		int hash = cast<int>(temp ^ temp >> 16 ^ temp >> 8);
		return ( hash & table->size - 1, hash );
	}

	public index( V value ) => ( boolean found )
	{
		return contains(value);
	}
	public index( V value, boolean add ) => ()
	{
		if ( add )
			this.add(value);
		else
			remove(value);
	}

	public add( V value ) => ( boolean added )
	{		
		( int index, int hash ) = this.hash(value);
		try
		{
			Node current = check(table[index]);
			while ( current->hash != hash and current->value != value )
				current = check(current->next);
			return false;
		}
		recover
		{
			table[index] = Node:create(hash, value, table[index]);
			modifications += 1;
			return true;
		}
	}
	public remove( V value ) => ( boolean removed )
	{	
		( int index, int hash ) = this.hash(value);
		try
		{
			nullable Node last = null;
			Node current = check(table[index]);
			while ( current->hash != hash and current->value != value )
				( last, current ) = ( current, check(current->next) );
			try
			{
				check(last)->next = current->next;
			}
			recover
			{
				table[index] = current->next;
			}
			modifications += 1;
			return true;
		}
		recover
		{
			return false;
		}
	}
	public readonly contains( V value ) => ( boolean found )
	{		
		( int index, int hash ) = this.hash(value);
		try
		{
			readonly Node current = check(table[index]);			
			while ( current->hash != hash and current->value != value )
				current = check(current->next);
			return true;
		}
		recover
		{
			return false;
		}
	}

	public immutable iterator() => ( Iterator<V> )
	{
		return HashSetIterator:create();
	}
	
	public clear() => ()
	{
		size = 0;
		table = Node:create[256];
	}
	
	
	private class HashSetIterator implements Iterator<V>
	{
		int index = 0;
		nullable Node current;
		int expectedModifications = modifications;
		public create()
		{
			current = next(null);
		}
		public next( nullable Node current ) => ( nullable Node )
		{
			try
			{
				return check(check(current)->next);
			}
			recover
			{
				while ( current === null and index < table->size )
				{
					current = table[index];
					index += 1;
				}
				return current;
			}
		}

		private readonly checkForModifications() => ()
		{
			if ( modifications != expectedModifications )
				throw IllegalModificationException:create();
		}

		public readonly hasNext() => ( boolean )
		{
			checkForModifications();
			return current !== null;
		}

		public next() => ( V value )
		{
			checkForModifications();
			try
			{				 
				( nullable V value, current ) = ( check(current)->value, next(current) );
				return check(value);
			}
			recover
			{
				throw IndexOutOfBoundsException:create();
			}
		}
	}
}
