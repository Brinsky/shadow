immutable class shadow.standard@Class
{
	String name = "";
	get nullable Class parent = null;
	Class[] interfaces = Class:create[0];
	Object[] data = Object:create[0];
	int size, width, flags;

	private create() { }
	protected native allocate() => ( Object );
	protected native allocate( int size ) => ( Object );
	protected interfaceData( Class interfaceClass ) => ( nullable Object )
	{
		for ( int i = 0; i < interfaces->size; i += 1 )
			if ( interfaces[i] === interfaceClass )
				return data[i];
	}

	public isInterface() => ( boolean )
	{
		return (flags & 0b01) != 0;
	}
	public isPrimitive() => ( boolean )
	{
		return (flags & 0b10) != 0;
	}

	public isSubtype( Class other ) => ( boolean )
	{
		if ( this == other )
			return true;
		if ( !other.isInterface() )
			try
			{
				return check(parent).isSubtype(other);
			}
			recover
			{
			}
		else for ( int i = 0; i < interfaces->size; i += 1 )
			try
			{
				if ( check(interfaces[i]).isSubtype(other) )
					return true;
			}
			recover
			{
			}
		return false;
	}

	public toString() => ( String )
	{
		return name;
	}
}
