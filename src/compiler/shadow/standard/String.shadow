immutable final class shadow.standard@String
implements CanCompare<String>, CanIterate<String>
{
	ubyte[] data = ubyte:create[0];
	public create()
	{
	}

	public getByte( int index ) => ( ubyte )
	{
		return data[index];
	}
	public get length() => ( int )
	{
		return data->length;
	}

	public equals( String other ) => ( boolean )
	{
		if ( this->length != other->length )
			return false;
		for ( int i = 0; i < this->length; i += 1 )
			if ( getByte(i) != other.getByte(i) )
				return false;
		return true;
	}
	public compare( String other ) => ( int )
	{
		for ( int i = 0; i < this->length and i < other->length; i += 1 )
			if ( getByte(i) != other.getByte(i) )
				return getByte(i) - other.getByte(i);
		return this->length - other->length;
	}

	public toString() => ( String )
	{
		return this;
	}
}
