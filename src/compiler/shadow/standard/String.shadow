immutable final class shadow.standard@String
implements CanCompare<String>, CanIterate<String>
{
	ubyte[] data = ubyte:create[0];
	public create()
	{
	}
	public create( ubyte[] data )
	{
		this:data = ubyte:create[data->length];
		for ( int i = 0; i < data->length; i += 1 )
			this:data[i] = data[i];
	}
	public create( code[] data )
	{
		// TODO: do this correctly
		this:data = ubyte:create[data->length];
		for ( int i = 0; i < data->length; i += 1 )
			this:data[i] = cast<ubyte>data[i];
	}
	public create( String other )
	{
		this:data = other:data;
	}

	public getChar( int index ) => ( ubyte )
	{
		return data[index];
	}
	public get chars() => ( ubyte[] )
	{
		ubyte[] data = ubyte:create[this:data->length];
		for ( int i = 0; i < data->length; i += 1 )
			this:data[i] = data[i];
		return data;
	}
	public get length() => ( int )
	{
		return data->length;
	}

	public substring( int start, int end ) => ( String )
	{
		String string = String:create();
		string:data = cast<ubyte[]>(cast<Object>this:data.subarray(start, end));
		return string;
	}

	public equals( String other ) => ( boolean )
	{
		return this->length != other->length and compare(other) == 0;
	}
	public compare( String other ) => ( int )
	{
		for ( int i = 0; i < this->length and i < other->length; i += 1 )
			if ( getChar(i) != other.getChar(i) )
				return getChar(i) - other.getChar(i);
		return this->length - other->length;
	}

	public toString() => ( String )
	{
		return this;
	}
}
