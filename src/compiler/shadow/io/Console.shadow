singleton shadow.io@Console
{
	boolean wasLF = false;
	public create()
	{
		init();
	}
	private native init() => ();

	public readLine() => ( String, boolean eof )
	{
		var string = MutableString:create();
//		code c;
//		boolean eof;
//		( c, eof ) = readCode();
		( code c, boolean eof ) = readCode();
		if ( wasLF and c == '\n' )
			( c, eof ) = readCode();
		while ( c != '\n' and c != '\r' and !eof )
		{
			string.append(c);
			( c, eof ) = readCode();
		}
		wasLF = ( c == '\r' );
		return ( #string, eof );
	}
	public readCode() => ( code, boolean eof )
	{
		int value;
		boolean eof;
		( value, eof ) = readByte();
		if ( value < 0 )
		{
			int bytes = 0;
			byte mask = 1y << 7y >> 1y;
			while ( value >= mask )
			{
				bytes += 1;
				mask >>= 1y;
			}
			value &= ~mask;
			while ( bytes > 0 )
			{
				value <<= 6;
				byte b;
				( b, eof ) = readByte();
				value |= b & 0b00111111y;
				bytes -= 1;
			}
		}
		return ( cast<code>(value), eof );
	}
	public native readByte() => ( byte, boolean eof );

	public print( nullable Object value ) => ( Console )
	{
		return print(#value);
	}
	public printLine( nullable Object value ) => ( Console )
	{
		return print(value).printLine();
	}
	public native print( String value ) => ( Console );
	public native printLine() => ( Console );

	public printError( nullable Object value ) => ( Console )
	{
		return printError(#value);
	}
	public printErrorLine( nullable Object value ) => ( Console )
	{
		return printError(value).printErrorLine();
	}
	public native printError( String value ) => ( Console );
	public native printErrorLine() => ( Console );
}
