immutable singleton shadow.io@Console
{
	boolean wasLF = false;
	public readLine() => ( String )
	{
		auto string = MutableString:create();
		code c = readCode();
		if ( wasLF and c == '\n' )
			c = readCode();
		while ( c != '\n' and c != '\r' )
		{
			string.append(c);
			c = readCode();
		}
		wasLF = ( c == '\r' );
		return #string;
	}
	public native readCode() => ( code );

	public print( nullable Object value ) => ( Console )
	{
		return print(#value);
	}
	public printLine( nullable Object value ) => ( Console )
	{
		return print(value).printLine();
	}
	public native print( String value ) => ( Console );
	public native printLine() => ( Console );

	public printError( nullable Object value ) => ( Console )
	{
		return printError(#value);
	}
	public printErrorLine( nullable Object value ) => ( Console )
	{
		return printError(value).printErrorLine();
	}
	public native printError( String value ) => ( Console );
	public native printErrorLine() => ( Console );
}
